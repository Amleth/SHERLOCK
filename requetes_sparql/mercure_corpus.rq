# AFFICHER LES LIVRAISONS ET LEUR AUTEUR, TRIEES PAR DATE ET PAR TITRE

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select distinct ?F2_livraison ?titre ?date ?nom where { 
	?F18 rdf:type lrmoo:F18_Serial_Work .
	?F27 lrmoo:R16_initiated ?F18 .
	?F27 crm:P14_carried_out_by ?auteur .
	?auteur crm:P1_is_identified_by ?nom .
	?F18 lrmoo:R10_has_member ?F1_livraison .
    ?F1_livraison crm:P1_is_identified_by ?titre .
    ?F1_livraison lrmoo:R3_is_realised_in ?F2_livraison .
    ?E63 crm:P92_brought_into_existence ?F2_livraison .
    ?E63 crm:P4_has_time-span/crm:P80_end_is_qualified_by ?date .
} 

ORDER BY ?date ?titre

# AFFICHER LES ARTICLES TRIES PAR DATE ET PAR TITRE

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select distinct ?F2_livraison ?titre ?date ?article ?titre_article where { 
	?F18 rdf:type lrmoo:F18_Serial_Work .
	?F18 lrmoo:R10_has_member ?F1_livraison .
    ?F1_livraison crm:P1_is_identified_by ?titre .
    ?F1_livraison lrmoo:R3_is_realised_in ?F2_livraison .
    ?F1_livraison lrmoo:R10_has_member ?article .
    ?article crm:P1_is_identified_by ?titre_article .
    ?E63 crm:P92_brought_into_existence ?F2_livraison .
    ?E63 crm:P4_has_time-span/crm:P80_end_is_qualified_by ?date .
} 

ORDER BY ?date ?titre

# AFFICHER LES NOMS DE PERSONNES TRIES PAR ORDRE ALPHABETIQUE

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select distinct ?personne ?nom ?autrenom where { 
    ?E32 rdf:type crm:E32_Authority_Document .
    ?E32 crm:P1_is_identified_by "Noms de personnes" .
    ?E32 crm:P71_lists ?personne .
    ?personne crm:P1_is_identified_by ?E41 .
    ?E41 rdfs:label ?nom .
    ?E41 crm:P139_has_alternative_form/rdfs:label ?autrenom .
} 

ORDER BY ?nom
LIMIT 100