#######################################################################
#### REQUETES SELECT
#######################################################################

# AFFICHER LES LIVRAISONS ET LEUR AUTEUR, TRIEES PAR DATE ET PAR TITRE

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select distinct ?F2_livraison ?titre ?date ?nom where { 
	?F18 a lrmoo:F18_Serial_Work ;
         lrmoo:R10_has_member ?F1_livraison .
	?F27 lrmoo:R16_initiated ?F18 ;
	     crm:P14_carried_out_by ?auteur .
	?auteur crm:P1_is_identified_by ?nom .
    ?F1_livraison crm:P1_is_identified_by ?titre ;
                  lrmoo:R3_is_realised_in ?F2_livraison .
    ?E63 crm:P92_brought_into_existence ?F2_livraison ;
         crm:P4_has_time-span/crm:P80_end_is_qualified_by ?date .
} 

ORDER BY ?date ?titre

# AFFICHER LE FACSIMILE D'UNE LIVRAISON

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>

select distinct ?F2_livraison ?titre ?date ?D1 where { 
	?F18 a lrmoo:F18_Serial_Work ;
         lrmoo:R10_has_member ?F1_livraison .
    ?F1_livraison crm:P1_is_identified_by ?titre ;
                  lrmoo:R3_is_realised_in ?F2_livraison .
    ?F3_livraison lrmoo:R4_embodies ?F2_livraison .
    ?F5_livraison lrmoo:R7_is_materialization_of ?F3_livraison .
    ?D2 crmdig:L1_digitized ?F5_livraison ;
        crmdig:L11_had_output ?D1 .
    ?E63 crm:P92_brought_into_existence ?F2_livraison ;
         crm:P4_has_time-span/crm:P80_end_is_qualified_by ?date .
} 

ORDER BY ?date ?titre

# AFFICHER LES ARTICLES TRIES PAR DATE ET PAR TITRE

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select distinct ?F2_livraison ?titre ?date ?article ?titre_article where { 
	?F18 a lrmoo:F18_Serial_Work ;
         lrmoo:R10_has_member ?F1_livraison .
    ?F1_livraison crm:P1_is_identified_by ?titre ;
                  lrmoo:R3_is_realised_in ?F2_livraison ;
                  lrmoo:R10_has_member ?article .
    ?article crm:P1_is_identified_by ?titre_article .
    ?E63 crm:P92_brought_into_existence ?F2_livraison ;
         crm:P4_has_time-span/crm:P80_end_is_qualified_by ?date .
} 

ORDER BY ?date ?titre

# AFFICHER LES NOMS DE PERSONNES TRIES PAR ORDRE ALPHABETIQUE

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?personne ?nom ?autrenom where { 
    ?E32 rdf:type crm:E32_Authority_Document .
    ?E32 crm:P1_is_identified_by "Noms de personnes" .
    ?E32 crm:P71_lists ?personne .
    ?personne crm:P1_is_identified_by ?E41 .
    ?E41 rdfs:label ?nom .
    ?E41 crm:P139_has_alternative_form/rdfs:label ?autrenom .
} 

ORDER BY ?nom
LIMIT 100

# AFFICHER LES NOTES ASSOCIEES AUX NOMS DE PERSONNES

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX dcterms: <http://purl.org/dc/terms/>

select distinct ?E21 ?nom ?note ?date ?firstname ?lastname where {
    
    ?E21 a crm:E21_Person ;
    	 crm:P1_is_identified_by/rdfs:label ?nom .
    ?E13 crm:P140_assigned_attribute_to ?E21 ;
    	 crm:P14_carried_out_by/crm:P1_is_identified_by/crmdig:L51_has_first_name ?firstname ;
         crm:P14_carried_out_by/crm:P1_is_identified_by/crmdig:L52_has_last_name ?lastname ;
         crm:P141_assigned/rdfs:label ?note .
         #dcterms:created ?date .
    
} 

ORDER BY ?nom
LIMIT 100

# AFFICHER LES INSTITUTIONS ET LEUR SOUS-INSTITUTIONS

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select distinct ?groupe ?nom ?sousgroupe ?sousgroupenom where { 

	?groupe a crm:E74_Group ;
            crm:P1_is_identified_by/rdfs:label ?nom .
    ?groupe crm:P107_has_current_or_former_member ?sousgroupe .
    ?sousgroupe crm:P1_is_identified_by/rdfs:label ?sousgroupenom .
    
}

ORDER BY ?nom


#######################################################################
#### REQUETES INSERT
#######################################################################

# AJOUT D'UN LIEU

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
BASE <http://data-iremus.huma-num.fr/id/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

INSERT DATA
  { 
    GRAPH <http://data-iremus.huma-num.fr/graph/ancien_regime/lieux> 
      { 
        <idfacticeE93> a crm:E93_Presence ;
                       dcterms:created "2017-07-07"^^xsd:date ;
                       dcterms:creator <635f1b1b-322e-4958-88e6-45d1efaed0db> ;
                       crm:P1_is_identified_by <idfacticeE41> .
        <idfacticeE41> rdfs:label "Lieu factice" ;
                       crm:P139_has_alternative_form <idfacticeE41alt> .
        <idfacticeE41alt> rdfs:label "Lieu factice synonyme" .
      } 
  }


# ANNOTER UN LIEU (AJOUTER UN NOM)

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
BASE <http://data-iremus.huma-num.fr/id/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
 
INSERT DATA
  { 
    GRAPH <http://data-iremus.huma-num.fr/graph/ancien_regime/lieux> 
      { 
        <idfacticeE13> a crm:E13_Attribute_Assignment ;
        			   crm:P14_carried_out_by <635f1b1b-322e-4958-88e6-45d1efaed0db> ;
                       crm:P140_assigned_attribute_to <idfacticeE93> ;
                       crm:P141_assigned <idfacticeE41> ;
                       crm:P177_assigned_property__type crm:P1_is_identified_by .
        <idfacticeE41> rdfs:label "nom factice" .
      } 
  }

  # ANNOTER UN LIEU (MODIFIER LE NOM)

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
BASE <http://data-iremus.huma-num.fr/id/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
 
INSERT DATA
  { 
    GRAPH <http://data-iremus.huma-num.fr/graph/ancien_regime/lieux> 
      { 
        <idfacticeE13> a crm:E13_Attribute_Assignment ;
        			   crm:P14_carried_out_by <635f1b1b-322e-4958-88e6-45d1efaed0db> ;
                       crm:P140_assigned_attribute_to <idfacticeE41> ;
                       crm:P141_assigned "nom modifi√©" ;
                       crm:P177_assigned_property__type rdfs:label .
      } 
  }

  # TROUVER TOUS LES NOMS D'UN LIEU

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX lrmoo: <http://www.cidoc-crm.org/lrmoo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
BASE <http://data-iremus.huma-num.fr/id/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
 
SELECT ?lieu ?nom 
FROM <http://data-iremus.huma-num.fr/graph/ancien_regime/lieux>
WHERE {
    
     ?lieu a crm:E93_Presence ;
          crm:P1_is_identified_by/rdfs:label ?nom .
	FILTER (?lieu = <idfacticeE93>) 
    
}

ORDER BY ?nom